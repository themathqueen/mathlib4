/-
Copyright (c) 2025 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
module

public import Mathlib.Algebra.Star.LinearMap
public import Mathlib.Analysis.InnerProductSpace.Positive
public import Mathlib.Analysis.Matrix.Order

/-! The GNS-inner product on matrices

This file shows that the GNS-inner product space on matrices is equivalent
to `Matrix.toMatrixInnerProductSpace`.
So choosing a positive linear map `Matrix n n ‚ÑÇ ‚Üí‚Çö[‚ÑÇ] ‚ÑÇ` is equivalent to choosing
a positive semi-definite matrix `Q`. -/

@[expose] public section

variable {n m R ùïú : Type*} [Fintype n] [DecidableEq n] [RCLike ùïú]

open scoped MatrixOrder ComplexOrder

namespace Matrix

set_option backward.privateInPublic true in
/-- The pre-inner product space structure implementation. Only an auxiliary for
`Matrix.toMatrixSeminormedAddCommGroup`, `Matrix.toMatrixNormedAddCommGroup`,
and `Matrix.toMatrixInnerProductSpace`. -/
private abbrev PosSemidef.matrixPreInnerProductSpace {M : Matrix n n ùïú} (hM : M.PosSemidef) :
    PreInnerProductSpace.Core ùïú (Matrix n n ùïú) where
  inner x y := (y * M * x·¥¥).trace
  conj_inner_symm _ _ := by
    simp only [mul_assoc, starRingEnd_apply, ‚Üê trace_conjTranspose, conjTranspose_mul,
      conjTranspose_conjTranspose, hM.isHermitian.eq]
  re_inner_nonneg x := RCLike.nonneg_iff.mp (hM.mul_mul_conjTranspose_same x).trace_nonneg |>.1
  add_left := by simp [mul_add]
  smul_left := by simp

set_option backward.privateInPublic true in
set_option backward.privateInPublic.warn false in
/-- A positive definite matrix `M` induces a norm on `Matrix n n ùïú`
`‚Äñx‚Äñ = sqrt (x * M * x·¥¥).trace`. -/
noncomputable def toMatrixSeminormedAddCommGroup (M : Matrix n n ùïú) (hM : M.PosSemidef) :
    SeminormedAddCommGroup (Matrix n n ùïú) :=
  @InnerProductSpace.Core.toSeminormedAddCommGroup _ _ _ _ _ hM.matrixPreInnerProductSpace

set_option backward.privateInPublic true in
set_option backward.privateInPublic.warn false in
/-- A positive definite matrix `M` induces a norm on `Matrix n n ùïú`:
`‚Äñx‚Äñ = sqrt (x * M * x·¥¥).trace`. -/
noncomputable def toMatrixNormedAddCommGroup (M : Matrix n n ùïú) (hM : M.PosDef) :
    NormedAddCommGroup (Matrix n n ùïú) :=
  letI : InnerProductSpace.Core ùïú (Matrix n n ùïú) :=
  { __ := hM.posSemidef.matrixPreInnerProductSpace
    definite x hx := by
      classical
      obtain ‚ü®y, hy, rfl‚ü© := CStarAlgebra.isStrictlyPositive_iff_eq_star_mul_self.mp
        hM.isStrictlyPositive
      simp only at hx
      rw [‚Üê mul_assoc, ‚Üê conjTranspose_conjTranspose x, star_eq_conjTranspose, ‚Üê conjTranspose_mul,
        conjTranspose_conjTranspose, mul_assoc, trace_conjTranspose_mul_self_eq_zero_iff] at hx
      lift y to (Matrix n n ùïú)À£ using hy
      simpa [‚Üê mul_assoc] using congr(y‚Åª¬π * $hx) }
  this.toNormedAddCommGroup

/-- A positive semi-definite matrix `M` induces an inner product on `Matrix n n ùïú`:
`‚ü™x, y‚ü´ = (y * M * x·¥¥).trace`. -/
def toMatrixInnerProductSpace (M : Matrix n n ùïú) (hM : M.PosSemidef) :
    letI : SeminormedAddCommGroup (Matrix n n ùïú) := M.toMatrixSeminormedAddCommGroup hM
    InnerProductSpace ùïú (Matrix n n ùïú) :=
  InnerProductSpace.ofCore _

@[deprecated (since := "2025-11-18")] alias PosDef.matrixNormedAddCommGroup :=
  toMatrixNormedAddCommGroup
@[deprecated (since := "2025-11-12")] alias PosDef.matrixInnerProductSpace :=
  toMatrixInnerProductSpace

open Matrix

/-- The *weight* of a linear functional `f` on matrices is the matrix such that
`f a = trace (a * f.weight)` for all `a`. -/
def Module.Dual.weight [Semiring R] (f : Module.Dual R (Matrix n n R)) :
    Matrix n n R := ‚àë i, ‚àë j, .single j i (f (.single i j 1))

theorem Module.Dual.eq_trace_mul_weight [Semiring R] (f : Module.Dual R (Matrix n n R))
    (a : Matrix n n R) : f a = (a * f.weight).trace := by
  conv_lhs => rw [matrix_eq_sum_single a]
  simp_rw [fun i j ‚Ü¶ show single i j (a i j) = a i j ‚Ä¢ single i j 1 by simp,
    map_sum, map_smul]
  simp [weight, Finset.mul_sum, trace, sum_apply, mul_apply,
    Matrix.single_apply, ite_and]

/-- `f` is star-preserving iff its weight is Hermitian. -/
theorem Module.Dual.isSelfAdjoint_toConv_iff_isHermitian_weight
    [CommSemiring R] [StarRing R] (f : Module.Dual R (Matrix n n R)) :
    IsSelfAdjoint (WithConv.toConv f) ‚Üî f.weight.IsHermitian := by
  simp only [IsSelfAdjoint, WithConv.ext_iff, LinearMap.ext_iff, LinearMap.intrinsicStar_apply]
  simp_rw [eq_trace_mul_weight, star_eq_conjTranspose, ‚Üê trace_conjTranspose, conjTranspose_mul,
    conjTranspose_conjTranspose, trace_mul_comm _ f.weight, ‚Üê ext_iff_trace_mul_right, IsHermitian]

open scoped ComplexOrder MatrixOrder

-- `f` is a positive and star-preserving iff `f.weight` is positive semi-definite
-- when `R = ‚ÑÇ`, we can drop `toConv f` is self-adjoint (but a lot of missing API ugh)
theorem Module.Dual.monotone_and_isSelfAdjoint_toConv_iff_posSemidef_weight
    {ùïú : Type*} [RCLike ùïú] (f : Module.Dual ùïú (Matrix n n ùïú)) :
    Monotone f ‚àß IsSelfAdjoint (WithConv.toConv f) ‚Üî f.weight.PosSemidef := by
  rw [posSemidef_iff_dotProduct_mulVec, isSelfAdjoint_toConv_iff_isHermitian_weight,
    and_comm, and_congr_right_iff]
  intro h
  simp_rw [fun y ‚Ü¶ show star y ‚¨ù·µ• f.weight *·µ• y = (f.weight * vecMulVec y (star y)).trace by
    rw [vecMulVec_eq Unit, trace_mul_cycle', ‚Üê replicateCol_mulVec, trace_mul_comm,
      trace_replicateCol_mul_replicateRow, dotProduct_comm]]
  refine ‚ü®fun hs ‚Ü¶ ?_, fun H x y ‚Ü¶ ?_‚ü©
  ¬∑ simp only [Monotone, eq_trace_mul_weight] at hs
    intro y
    simpa [trace_mul_comm f.weight] using hs (posSemidef_vecMulVec_self_star y).nonneg
  ¬∑ rw [‚Üê sub_nonneg, ‚Üê sub_nonneg (b := f x), ‚Üê map_sub, eq_trace_mul_weight,
      trace_mul_comm]
    intro h
    obtain ‚ü®m, e, he‚ü© := posSemidef_iff_eq_sum_vecMulVec.mp h.posSemidef
    simp [he, Matrix.mul_sum]
    exact Finset.sum_nonneg fun _ _ ‚Ü¶ H _

end Matrix
