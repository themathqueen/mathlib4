/-
Copyright (c) 2025 Gregory Wickham. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gregory Wickham
-/
module

public import Mathlib.Analysis.CStarAlgebra.PositiveLinearMap
public import Mathlib.Analysis.InnerProductSpace.Adjoint
public import Mathlib.Analysis.InnerProductSpace.Completion
public import Mathlib.Topology.Algebra.LinearMapCompletion

/-!
# The GNS (Gelfand-Naimark-Segal) construction

This file contains the constructions and definitions that produce a ‚ãÜ-homomorphism from an arbitrary
C‚ãÜ-algebra into the algebra of bounded linear operators on an appropriately constructed Hilbert
space.

## Main results

- `f.PreGNS` : a type synonym of `A` that bundles in a fixed positive linear functional `f` so that
  we can construct an inner product and inner product-induced norm.
- `f.GNS` : the Hilbert space completion of `f.preGNS`.
- `f.gnsNonUnitalStarAlgHom` : The non-unital ‚ãÜ-homomorphism from a non-unital `A` into the bounded
  linear operators on `f.GNS`.
- `f.gnsStarAlgHom` : The unital ‚ãÜ-homomorphism from a unital `A` into the bounded linear operators
  on `f.GNS`.

## TODO

- Explicitly construct a unit norm cyclic vector Œ∂ such that
  a ‚Ü¶ ‚ü®(f.gns(NonUnital)StarAlgHom a) \* Œ∂, ,Œ∂‚ü© is a state on `A` for both unital and non-unital
  cases.

-/

@[expose] public section
open scoped ComplexOrder InnerProductSpace
open Complex ContinuousLinearMap UniformSpace Completion

variable {A : Type*} [NonUnitalCStarAlgebra A] [PartialOrder A] (f : A ‚Üí‚Çö[‚ÑÇ] ‚ÑÇ)

namespace PositiveLinearMap

set_option linter.unusedVariables false in
/-- The Gelfand‚îÄNaimark‚îÄSegal (GNS) space constructed from a positive linear functional on a
non-unital C‚ãÜ-algebra. This is a type synonym of `A`.

This space is only a pre-inner product space. Its Hilbert space completion is
`PositiveLinearMap.GNS`. -/
@[nolint unusedArguments]
def PreGNS (f : A ‚Üí‚Çö[‚ÑÇ] ‚ÑÇ) := A

instance : AddCommGroup f.PreGNS := inferInstanceAs (AddCommGroup A)
instance : Module ‚ÑÇ f.PreGNS := inferInstanceAs (Module ‚ÑÇ A)

/-- The map from the C‚ãÜ-algebra to the GNS space, as a linear equivalence. -/
def toPreGNS : A ‚âÉ‚Çó[‚ÑÇ] f.PreGNS := LinearEquiv.refl ‚ÑÇ _

/-- The map from the GNS space to the C‚ãÜ-algebra, as a linear equivalence. -/
def ofPreGNS : f.PreGNS ‚âÉ‚Çó[‚ÑÇ] A := f.toPreGNS.symm

@[simp]
lemma toPreGNS_ofPreGNS (a : f.PreGNS) : f.toPreGNS (f.ofPreGNS a) = a := rfl

@[simp]
lemma ofPreGNS_toPreGNS (a : A) : f.ofPreGNS (f.toPreGNS a) = a := rfl

variable [StarOrderedRing A]

/--
The (semi-)inner product space whose elements are the elements of `A`, but which has an
inner product-induced norm that is different from the norm on `A` and which is induced by `f`.
-/
abbrev preGNSpreInnerProdSpace : PreInnerProductSpace.Core ‚ÑÇ f.PreGNS where
  inner a b := f (star (f.ofPreGNS a) * f.ofPreGNS b)
  conj_inner_symm := by simp [‚Üê Complex.star_def, ‚Üê map_star f]
  re_inner_nonneg _ := RCLike.nonneg_iff.mp (f.map_nonneg (star_mul_self_nonneg _)) |>.1
  add_left _ _ _ := by rw [map_add, star_add, add_mul, map_add]
  smul_left := by simp [smul_mul_assoc]

noncomputable instance : SeminormedAddCommGroup f.PreGNS :=
  InnerProductSpace.Core.toSeminormedAddCommGroup (c := f.preGNSpreInnerProdSpace)
noncomputable instance : InnerProductSpace ‚ÑÇ f.PreGNS :=
  InnerProductSpace.ofCore f.preGNSpreInnerProdSpace

lemma preGNS_inner_def (a b : f.PreGNS) :
    ‚ü™a, b‚ü´_‚ÑÇ = f (star (f.ofPreGNS a) * f.ofPreGNS b) := rfl

lemma preGNS_norm_def (a : f.PreGNS) :
    ‚Äña‚Äñ = ‚àö(f (star (f.ofPreGNS a) * f.ofPreGNS a)).re := rfl

lemma preGNS_norm_sq (a : f.PreGNS) :
    ‚Äña‚Äñ ^ 2 = f (star (f.ofPreGNS a) * f.ofPreGNS a) := by
  have : 0 ‚â§ f (star (f.ofPreGNS a) * f.ofPreGNS a) := map_nonneg f <| star_mul_self_nonneg _
  rw [preGNS_norm_def, ‚Üê ofReal_pow, Real.sq_sqrt]
  ¬∑ rw [conj_eq_iff_re.mp this.star_eq]
  ¬∑ rwa [re_nonneg_iff_nonneg this.isSelfAdjoint]

/--
The Hilbert space constructed from a positive linear functional on a C‚ãÜ-algebra.
-/
abbrev GNS := UniformSpace.Completion f.PreGNS

/--
The continuous linear map from a C‚ãÜ-algebra `A` to the `PositiveLinearMap.preGNS` space induced by
a positive linear functional `f : A ‚Üí‚Çö[‚ÑÇ] ‚ÑÇ`. This map is given by left-multiplication by `a`:
`x ‚Ü¶ f.toPreGNS (a * f.ofPreGNS x)`.

This is the map that is lifted to the completion of `f.PreGNS` (i.e. `f.GNS`) in order to define
`gnsNonUnitalStarAlgHom`.
-/
@[simps!]
noncomputable def leftMulMapPreGNS (a : A) : f.PreGNS ‚ÜíL[‚ÑÇ] f.PreGNS :=
  f.toPreGNS.toLinearMap ‚àò‚Çó mul ‚ÑÇ A a ‚àò‚Çó f.ofPreGNS.toLinearMap |>.mkContinuous ‚Äña‚Äñ fun x ‚Ü¶ by
    rw [‚Üê sq_le_sq‚ÇÄ (by positivity) (by positivity), mul_pow, ‚Üê RCLike.ofReal_le_ofReal (K := ‚ÑÇ),
      RCLike.ofReal_pow, RCLike.ofReal_eq_complex_ofReal, preGNS_norm_sq]
    have : star (f.ofPreGNS x) * star a * (a * f.ofPreGNS x) ‚â§
        ‚Äña‚Äñ ^ 2 ‚Ä¢ star (f.ofPreGNS x) * f.ofPreGNS x := by
      rw [‚Üê mul_assoc, mul_assoc _ (star a), sq, ‚Üê CStarRing.norm_star_mul_self (x := a),
        smul_mul_assoc]
      exact CStarAlgebra.star_left_conjugate_le_norm_smul
    calc
      _ ‚â§ f (‚Äña‚Äñ ^ 2 ‚Ä¢ star (f.ofPreGNS x) * f.ofPreGNS x) := by
        simpa using OrderHomClass.mono f this
      _ = _ := by simp [‚Üê Complex.coe_smul, preGNS_norm_sq, smul_mul_assoc]

@[simp]
lemma leftMulMapPreGNS_mul_eq_comp (a b : A) :
    f.leftMulMapPreGNS (a * b) = f.leftMulMapPreGNS a ‚àòL f.leftMulMapPreGNS b := by
  ext c; simp [mul_assoc]

/--
The non-unital ‚ãÜ-homomorphism/‚ãÜ-representation of `A` into the algebra of bounded operators on
a Hilbert space that is constructed from a positive linear functional `f` on a possibly non-unital
C‚ãÜ-algebra.
-/
noncomputable def gnsNonUnitalStarAlgHom : A ‚Üí‚ãÜ‚Çô‚Çê[‚ÑÇ] (f.GNS ‚ÜíL[‚ÑÇ] f.GNS) where
  toFun a := (f.leftMulMapPreGNS a).completion
  map_smul' r a := by
    ext x
    induction x using Completion.induction_on with
    | hp => apply isClosed_eq <;> fun_prop
    | ih x => simp [smul_mul_assoc]
  map_zero' := by
    ext b
    induction b using Completion.induction_on with
    | hp => apply isClosed_eq <;> fun_prop
    | ih b => simp [Completion.coe_zero]
  map_add' x y := by
    ext c
    induction c using Completion.induction_on with
      | hp => apply isClosed_eq <;> fun_prop
      | ih c => simp [add_mul, Completion.coe_add]
  map_mul' a b := by
    ext c
    induction c using Completion.induction_on with
      | hp => apply isClosed_eq <;> fun_prop
      | ih c => simp
  map_star' a := by
    refine (eq_adjoint_iff (f.leftMulMapPreGNS (star a)).completion
      (f.leftMulMapPreGNS a).completion).mpr ?_
    intro x y
    induction x, y using Completion.induction_on‚ÇÇ with
    | hp => apply isClosed_eq <;> fun_prop
    | ih x y => simp [mul_assoc, preGNS_inner_def]

lemma gnsNonUnitalStarAlgHom_apply {a : A} :
    f.gnsNonUnitalStarAlgHom a = (f.leftMulMapPreGNS a).completion := rfl

@[simp]
lemma gnsNonUnitalStarAlgHom_apply_coe {a : A} {b : f.PreGNS} :
    f.gnsNonUnitalStarAlgHom a b = f.leftMulMapPreGNS a b := by
  simp [gnsNonUnitalStarAlgHom_apply]

variable {A : Type*} [CStarAlgebra A] [PartialOrder A] [StarOrderedRing A] (f : A ‚Üí‚Çö[‚ÑÇ] ‚ÑÇ)

@[simp]
private lemma gnsNonUnitalStarAlgHom_map_one : f.gnsNonUnitalStarAlgHom 1 = 1 := by
  ext b
  induction b using Completion.induction_on with
  | hp => apply isClosed_eq <;> fun_prop
  | ih b => simp [gnsNonUnitalStarAlgHom]

/--
The unital ‚ãÜ-homomorphism/‚ãÜ-representation of `A` into the algebra of bounded operators on a Hilbert
space that is constructed from a positive linear functional `f` on a unital C‚ãÜ-algebra.

This is the unital version of `gnsNonUnitalStarAlgHom`.
-/
@[simps]
noncomputable def gnsStarAlgHom : A ‚Üí‚ãÜ‚Çê[‚ÑÇ] (f.GNS ‚ÜíL[‚ÑÇ] f.GNS) where
  __ := f.gnsNonUnitalStarAlgHom
  map_one' := by simp
  commutes' r := by simp [Algebra.algebraMap_eq_smul_one]

/-- A version of `norm_apply_le_of_nonneg` for functionals. -/
lemma norm_apply_le (f : A ‚Üí‚Çö[‚ÑÇ] ‚ÑÇ) (x : A) : ‚Äñf x‚Äñ ‚â§ ‚Äñf 1‚Äñ * ‚Äñx‚Äñ := by
  have := by simpa [f.preGNS_norm_def, f.preGNS_inner_def] using
    norm_inner_le_norm (ùïú := ‚ÑÇ) (f.toPreGNS 1) (f.toPreGNS x)
  grw [this]
  simp only [Complex.re_eq_norm.mpr <| f.map_nonneg _, star_mul_self_nonneg, zero_le_one]
  rw [‚Üê sq_le_sq‚ÇÄ (by positivity) (by positivity)]
  simp only [mul_pow, norm_nonneg, Real.sq_sqrt]
  simp_rw [sq, mul_assoc]
  refine mul_le_mul_of_nonneg_left ?_ (by positivity)
  simp [‚Üê CStarRing.norm_star_mul_self, f.norm_apply_le_of_nonneg]

theorem opNorm_eq_norm_map_one (f : A ‚Üí‚Çö[‚ÑÇ] ‚ÑÇ) : ‚Äñf.toContinuousLinearMap‚Äñ = ‚Äñf 1‚Äñ := by
  refine le_antisymm (f.toContinuousLinearMap.opNorm_le_bound (by positivity) f.norm_apply_le) ?_
  by_cases! Subsingleton A
  ¬∑ simp [Subsingleton.eq_zero (1 : A)]
  simpa using f.toContinuousLinearMap.le_opNorm 1

end PositiveLinearMap
